buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "th.in.ffc"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {

        debug {
            storeFile file("debug.keystore")
        }

        release {
            Properties properties
            if (file('local.properties').exists()) {
                properties = new Properties()
                properties.load(rootProject.file('local.properties').newDataInputStream())
            }

            storeFile file("release.keystore")
            keyAlias 'wis@nectec'
            keyPassword properties?.getProperty("release.keypassword") ?:
                    System.getenv('RELEASE_KEY_PASSWORD')
            storePassword properties?.getProperty("release.keypassword") ?:
                    System.getenv('RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix " debug"
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            manifestPlaceholders = [google_map_key: 'AIzaSyC9K6xSWv4pGnTTmxzpYziOm_5q02yOI20']
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [google_map_key: 'AIzaSyBxLlvsh23x0AQkkVPEF1eP5aSKIVwgCY8']
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/ASL2.0"
        exclude "META-INF/NOTICE"
        exclude "META-INF/LICENSE"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile "com.google.android.gms:play-services-maps:10.0.0"
    compile 'com.google.android.gms:play-services-analytics:10.0.0'
    compile 'org.osmdroid:osmdroid-android:5.5:release@aar'
    compile 'org.osmdroid:osmdroid-third-party:5.5:release@aar'
    compile 'me.piruin:quickaction:2.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') { transitive = true; }
}

apply plugin: 'com.google.gms.google-services'
